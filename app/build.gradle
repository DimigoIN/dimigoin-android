plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "in.dimigo.dimigoin"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 7
        versionName "0.2.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            resValue("bool", "FIREBASE_ANALYTICS_DEACTIVATED", "true")
            resValue("bool", "FIREBASE_CRASHLYTICS_ENABLED", "false")
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlyticsCollectionEnabled: "true"]
            resValue("bool", "FIREBASE_ANALYTICS_DEACTIVATED", "false")
            resValue("bool", "FIREBASE_CRASHLYTICS_ENABLED", "true")
        }
    }
    compileOptions {
        coreLibraryDesugaringEnabled true

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }
}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.4.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.1'

    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.3'

    // KTX
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_ktx_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_ktx_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_ktx_version"

    // Koin
    implementation "org.koin:koin-android-viewmodel:$koin_version"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:26.4.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    // Security
    implementation "androidx.security:security-crypto:1.1.0-alpha03"
    implementation 'androidx.biometric:biometric:1.1.0'

    // Skeleton
    implementation 'com.ethanhua:skeleton:1.1.2'
    implementation 'io.supercharge:shimmerlayout:2.1.0'

    // ETC
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation 'com.romandanylyk:pageindicatorview:1.0.3'
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    implementation 'tk.zielony:carbon:0.16.0.1'
    implementation 'com.google.zxing:core:3.4.1' // Barcode generator
}
